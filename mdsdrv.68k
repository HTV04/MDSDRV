;======================================================================
; MDSDRV - mega drive 68k sound driver
; (C) 2019-2020 ian karlsson
;======================================================================

ASM68K = 1
ASMZ80 = 0
PACK_Z80 = 1

	include		"mdssub.inc"

;======================================================================
; ABI call
;----------------------------------------------------------------------
mds_top
	bsr.w	mds_init
	bsr.w	mds_update
	bsr.w	mds_request
	bsr.w	mds_command

;======================================================================
; ABI version string
;----------------------------------------------------------------------
version_str
	dc.b	"MDSDRV0.1 200613"
version_str_end

;======================================================================
; initialize sound driver
;----------------------------------------------------------------------
; INPUT
;	a0 - pointer to work area
;   a1 - pointer to request base table (sdtop)
;   a2 - pointer to PCM data
; TRASHES
;   a0-a1, d0-d1
mds_init
@zero		equr d0

	lea		6(a1),a1						;add offset to sdtop
	move.l	a1,w_sdtop(a0)					;set sdtop
	lea		mds_psg_init(pc),a1

	clr.l	@zero
	move.l	@zero,w_request(a0)				;clear request
	move.l	@zero,2+w_request(a0)
	move.l	@zero,w_tempo(a0)				;clear tempo
	move.l	@zero,2+w_tempo(a0)
	move.l	@zero,w_counter(a0)				;clear counter
	move.l	@zero,2+w_counter(a0)
	move.l	@zero,w_seq_step(a0)			;clear seq step
	move.l	@zero,2+w_seq_step(a0)
	move.l	@zero,w_volume(a0)				;clear trk volume
	move.l	@zero,2+w_volume(a0)
	move.l	@zero,w_tmask(a0)				;clear trk mask
	move.l	@zero,2+w_tmask(a0)
	move.l	@zero,w_chmask(a0)				;clear chmask
	move.l	@zero,2+w_chmask(a0)
	move.w	@zero,w_pcm_enable(a0)			;clear pcm
	move.w	@zero,w_fm3_mask(a0)			;clear fm3mask
	move.l	@zero,w_fm3_tl(a0)

	move.w	#128,d1							; NTSC tempo
	btst	#6,io_version
	beq.s	@set_tempo
	move.w	#107,d1							; PAL tempo
@set_tempo
	move.w	d1,w_gtempo(a0)					;set global tempo

	lea		w_track(a0),a1					;track addr
	move.w	#TCOUNT-1,d1
@next_track
	move.b	@zero,t_note_flag(a1)			;clear flag
	move.w	@zero,t_peg_addr(a1)
	move.l	a1,t_psg_eg_addr(a1)
	move.w	#$ff0f,t_psg_eg_pos(a1)
	move.b	#RCOUNT<<1,t_request_id(a1)		;channel is free

	lea		TSIZE(twork),twork				;next track
	dbra	d1,@next_track

	stop_z80
	bsr.w	mds_z80_init

mds_halt
	block_z80
	lea		sound_psg,a1
	move.b	#$9f,(a1)						;mute psg
	move.b	#$bf,(a1)
	move.b	#$df,(a1)
	move.b	#$ff,(a1)

	reset_z80
	start_z80
	rts

;======================================================================
; Sound request
;----------------------------------------------------------------------
; INPUT
;	a0 - pointer to work area
;	d0 - request sound number
;	d1 - request priority, range 0-3.
; TRASHES
;	d0,d1
mds_request
	ori.w	#(1<<rf_active)+(1<<rf_stop),d0
	andi.w	#3,d1
	lsl.w	d1
	move.w	d0,w_request(a0,d1)
	rts

;======================================================================
; Command request
;----------------------------------------------------------------------
; INPUT
;	a0 - pointer to work area
;	d0 - command #
;	d1 - command parameters
; OUTPUT
;   d0 - return value (if used)
; TRASHES
;	a0,a1,d0,d1
mds_command
	cmpi.b	#@cmd_max,d0						; max command
	bcc.s	@cmd_dummy
	lsl.w	#2,d0
	jmp		@cmd_table(pc,d0)
@cmd_dummy
	rts
@cmd_table
	bra.w	@get_cmd_count
	bra.w	@get_sound_count
	bra.w	@get_status
	bra.w	@get_version
	bra.w	@get_gtempo
	bra.w	@set_gtempo
	bra.w	@write_fm_port0
	bra.w	@write_fm_port1
@cmd_max	equ (*-@cmd_table)/4

;----------------------------------------------------------------------
; command 0x00 - get command count
;----------------------------------------------------------------------
@get_cmd_count
	move.w	#@cmd_max,d0
	rts

;----------------------------------------------------------------------
; command 0x01 - get sound count
;----------------------------------------------------------------------
@get_sound_count
	move.l	w_sdtop(a0),a0
	move.w	-2(a0),d0
	rts

;----------------------------------------------------------------------
; command 0x02 - get status
;----------------------------------------------------------------------
@get_status
	lsl.w	#1,d1
	move.w	w_tmask(a0,d0),d1
	rts

;----------------------------------------------------------------------
; command 0x03 - get version string
;----------------------------------------------------------------------
@get_version
	lea		version_str(pc),a0
	move.w	#version_str_end-version_str,d0
	rts

;----------------------------------------------------------------------
; command 0x04 - get global tempo
;----------------------------------------------------------------------
@get_gtempo
	move.w	w_gtempo(a0),d0
	rts

;----------------------------------------------------------------------
; command 0x05 - set global tempo
;----------------------------------------------------------------------
@set_gtempo
	move.w	d0,w_gtempo(a0)
	rts

;----------------------------------------------------------------------
; command 0x06 - write data to FM port 0
;----------------------------------------------------------------------
@write_fm_port0
	movem.l	zram/zreq,-(sp)
	bsr.s	@fm_prepare
	move.b	d0,$4000(zram)
	move.b	d1,$4001(zram)
	mds_z80_start
	movem.l	(sp)+,zram/zreq
	rts

@fm_prepare
	lea		z80_bus_request,zreq
	lea		z80_ram,zram
	move.w	d1,d0
	lsr.w	#8,d0
	mds_z80_wait_fm
	rts

;----------------------------------------------------------------------
; command 0x07 - write data to FM port 1
;----------------------------------------------------------------------
@write_fm_port1
	movem.l	zram/zreq,-(sp)
	bsr.s	@fm_prepare
	move.b	d0,$4002(zram)
	move.b	d1,$4003(zram)
	mds_z80_start
	movem.l	(sp)+,zram/zreq
	rts

;======================================================================
; update sound driver
;----------------------------------------------------------------------
; INPUT
;	a0 - pointer to work area
; TRASHES
;   a1-a6, d0-d7
mds_update
@reqdata	equr	d0
@counter	equr	d0
@seq_step	equr	d1
@gtempo		equr	d2

	clr.l	rnum
	lea		w_track(work),twork
	lea		z80_bus_request,zreq
	lea		z80_ram,zram

@next_request
	move.w	w_request(work,rnum),@reqdata
	bmi.w	mds_handle_request

;----------------------------------------------------------------------
mds_handle_request_return		equ *
;----------------------------------------------------------------------

	move.w	w_tmask(work,rnum),@reqdata
	beq.s	@request_done
	move.w	w_counter(work,rnum),@counter	;apply tempo
	add.w	w_tempo(work,rnum),@counter
	addq.w	#1,@counter

	move.w	w_gtempo(work),@gtempo
	clr.w	@seq_step

@sequence_tick
	cmp.w	@gtempo,@counter				;sequence tick
	bcs.s	@tick_done
	sub.w	@gtempo,@counter
	addq.w	#1,@seq_step
	bra.s	@sequence_tick

@tick_done
	move.w	@seq_step,w_seq_step(work,rnum)
	move.w	@counter,w_counter(work,rnum)

@request_done
	addq	#2,rnum
	cmpi.w	#RCOUNT<<1,rnum
	bne		@next_request

;----------------------------------------------------------------------
; update voices
;----------------------------------------------------------------------
mds_update_voices
	lea		w_track(work),twork				; first track's addr
	move.w	#TCOUNT-1,tnum

@next_voice
	move.w	t_note_flag(twork),flag
	bpl.s	@voice_not_enabled				;skip if not enabled
	swap	flag

	move.b	t_request_id(twork),rnum	;update sequence if needed
	move.w	w_seq_step(work,rnum),d4

;----------------------------------------------------------------------
mds_update_seq_return			equ	*
;----------------------------------------------------------------------

	dbra	d4,mds_update_seq

	lea		w_chmask(work),tmpa0			; channel mask
	move.b	t_channel_id(twork),chnid		;check priority
	move.b	t_request_id(twork),d0
	rept	RCOUNT-1
		beq.s	@has_priority
		move.w	(tmpa0)+,d1
		btst	chnid,d1
		bne.s	@no_priority
		subq.b	#2,d0						;next request
	endr
@has_priority
	bclr	#nf+nf_silence,flag
	beq.s	@no_silence
	bclr	#nf+nf_enabled,flag
@no_silence
	clr.w	d0
	add.b	chnid,chnid
	move.b	chnid,d0
	add.b	d0,d0
	add.b	chnid,d0
	jmp		@ch_update_table(pc,d0)
@no_priority
	st		t_last_pitch(twork)
	ori.l	#nm_restore<<nf,flag

;----------------------------------------------------------------------
mds_update_return				equ *
;----------------------------------------------------------------------

	swap	flag
	move.w	flag,t_note_flag(twork)
@voice_not_enabled
	lea		TSIZE(twork),twork				; next track
	dbra	tnum,@next_voice

	rts

;======================================================================
; channel update routine table
;----------------------------------------------------------------------
; Each routine must if needed save the flags to memory and jump to
; mds_update_return at the end.
@ch_mapping			macro
	moveq	\1,chnid
	bra.w	\2
	endm

@ch_update_table
	@ch_mapping		#0,mds_fm0_update		;0
	@ch_mapping		#1,mds_fm0_update		;1
	@ch_mapping		#2,mds_fm0_update		;2
	@ch_mapping		#4,mds_fm1_update		;3
	@ch_mapping		#5,mds_fm1_update		;4
	@ch_mapping		#6,mds_fm6_update		;5
	@ch_mapping		#-128,mds_psg_update	;6
	@ch_mapping		#-96,mds_psg_update		;7
	@ch_mapping		#-64,mds_psg_update		;8
	@ch_mapping		#-32,mds_psgn_update	;9
	@ch_mapping		#10,mds_dummy_update	;a
	@ch_mapping		#11,mds_dummy_update	;b
	@ch_mapping		#12,mds_dummy_update	;c
	@ch_mapping		#13,mds_dummy_update	;d
	@ch_mapping		#14,mds_dummy_update	;e
	@ch_mapping		#15,mds_dummy_update	;f

;======================================================================
; starts or stops song playback
;----------------------------------------------------------------------
; INPUT
;   twork - track's working area
;   d0 - request #
; OUTPUT
;   -
; TRASHES
;	d0-d3, tmpa0, tmpa1
mds_handle_request
@reqdata	equr	d0
@tempo		equr	d1						;global tempo
@zero		equr	d1
@tempreg	equr	d2

	bclr	#rf_stop,@reqdata
	bne.w	@stop_song						;stop already playing song

	bclr	#rf_active,@reqdata
	move.w	@reqdata,w_request(work,rnum)	;store request flag

	subq.w	#1,@reqdata						;subtract sound codes
	bmi.w	mds_handle_request_return			;0 to stop a track

	move.l	w_sdtop(work),tmpa0
	lea		mds_psg_init(pc),tmpa1

	cmp.w	-2(tmpa0),@reqdata				;check if out of bounds
	bcc.w	mds_handle_request_return

	move.w	w_gtempo(work),@tempo
	subq.w	#1,@tempo
	move.w	@tempo,w_tempo(work,rnum)		;initial tempo
	clr.w	@zero
	move.w	@zero,w_counter(work,rnum)
	move.w	@zero,w_seq_step(work,rnum)

	lsl.w	#2,@reqdata						;request table
	adda.l	0(tmpa0,@reqdata),tmpa0			;location of song header

	move.l	tmpa0,tbase
	adda.w	(tmpa0)+,tbase					;song base offset
	move.w	(tmpa0)+,w_tmask(work,rnum)		;track mask
	lea		w_track(work),twork
	move.w	#TCOUNT-1,tnum
	move.w	w_tmask(work,rnum),tmask

@next_track
	btst	tnum,tmask
	beq.w	@skip_track

	move.l	tbase,t_base_addr(twork)
	move.b	(tmpa0)+,@reqdata				;channel id
	move.b	#nm_init,t_note_flag(twork)
	move.b	(tmpa0)+,t_channel_flag(twork)
	lsl.w	#8,@reqdata
	or.w	rnum,@reqdata
	move.w	@reqdata,t_channel_id(twork)

	clr.w	@tempreg						;allocate channel
	bset	@reqdata,@tempreg
	or.w	@tempreg,w_chmask(work,rnum)

	cmp.w	#ct_psg<<8,@reqdata
	bcs.s	@not_psg
	move.b	@zero,t_psg_nmode(twork)		;noise mode initialize
	move.w	tmpa1,t_psg_eg_addr(twork)		;psg eg initialize
	move.w	#$ff0f,t_psg_eg_pos(twork)
	bra.s	@not_fm

@not_psg
	move.b	#$c0,t_fm_pan_lfo(twork)		;initial panning

@not_fm
	st		t_last_pitch(twork)				;previous pitch
	move.w	@zero,t_note(twork)				;note/transpose
	move.w	@zero,t_trs(twork)				;transpose/portamento
	move.w	#$8f00,t_vol(twork)				;vol/lfo_delay

	move.w	@zero,t_peg_addr(twork)			;pitch e.g.
	move.w	@zero,t_peg_mod(twork)			;pitch e.g.
	move.w	(tmpa0)+,t_position(twork)		;start position
	move.w	@zero,t_stack_pos(twork)		;stack_pos & counter
	move.w	#$0b0b,t_rest_time(twork)		;default time (-1)
	move.b	@zero,t_stack_pos(twork)

@skip_track
	lea		TSIZE(twork),twork				;next track
	dbra	tnum,@next_track
	bra.w	mds_handle_request_return

;======================================================================
; stops song playback
;----------------------------------------------------------------------
; INPUT
;   twork - track's working area
; OUTPUT
;   -
; TRASHES
;	d1,d6
@stop_song
	move.w	@reqdata,w_request(work,rnum)	;store request flag
	lea		w_track(work),twork
	move.w	#TCOUNT-1,tnum
	move.w	w_tmask(work,rnum),tmask

@stop_next_track
	btst	tnum,tmask						;track id
	beq.w	@stop_skip_track
	ori.b	#nm_stop,t_note_flag(twork)
	move.b	#RCOUNT<<1,t_request_id(twork)	;track is free

@stop_skip_track
	lea		TSIZE(twork),twork				;next track
	dbra	tnum,@stop_next_track
	clr.w	w_tmask(work,rnum)				;clear trackmask
	clr.w	w_chmask(work,rnum)				;clear chmask
	bra.w	mds_handle_request_return

;======================================================================
; update track sequences
;----------------------------------------------------------------------
; INPUT
;   twork,rnum - track's working area
; OUTPUT
;   -
; TRASHES
;	?
mds_update_seq
@trackpos	equr	d0
@cmd		equr	d1
@cmdlen		equr	d2
@tempreg	equr	d3
	subq.b	#1,t_counter(twork)
	bcc.w	mds_update_seq_return

;----------------------------------------------------------------------
; command read loop
;----------------------------------------------------------------------
@read_command
	move.l	t_base_addr(twork),tbase
	move.w	t_position(twork),@trackpos
	moveq	#1,@cmdlen

@next_command
	add.w	@cmdlen,@trackpos
	move.b	-1(tbase,@trackpos),@cmd
	bmi.s	@cmd_80

@cmd_rest
	bset	#nf+nf_key_off,flag
	bclr	#nf+nf_key_on,flag
	move.b	@cmd,t_rest_time(twork)			;00-7f - rest
	move.b	@cmd,t_counter(twork)
	move.w	@trackpos,t_position(twork)
	bra.w	mds_update_seq_return

@cmd_80
	sub.b	#$81,@cmd						;81 - tie
	beq.s	@cmd_tie
	addq.b	#1,@cmd
	beq.s	@cmd_rest2						;80 - rest (alternate)
	cmp.b	#$60,@cmd
	bcc.s	@cmd_other

	subq.b	#2,@cmd							;82-df - note
	move.b	@cmd,t_note(twork)
	bset	#nf+nf_key_on,flag
	btst	#nf+nf_slur,flag
	bne.s	@cmd_tie
	bset	#nf+nf_key_off,flag
	btst	#cf+cf_drum_mode,flag			;drum mode on?
	bne.s	@cmd_drum_mode

@cmd_tie
	lea		t_note_time(twork),tmpa0
@cmd_read_delta
	move.b	0(tbase,@trackpos),@cmd			;read next command
	bmi		@cmd_previous_delta

@cmd_new_delta
	move.b	@cmd,t_counter(twork)
	move.b	@cmd,(tmpa0)
	addq.w	#1,@trackpos
	move.w	@trackpos,t_position(twork)
	bra.w	mds_update_seq_return

@cmd_previous_delta
	move.b	(tmpa0),t_counter(twork)
	move.w	@trackpos,t_position(twork)
	bra.w	mds_update_seq_return

@cmd_rest2
	bset	#nf+nf_key_off,flag				;80 - rest 2
	bclr	#nf+nf_key_on,flag
	move.b	t_rest_time(twork),t_counter(twork)
	move.w	@trackpos,t_position(twork)
	bra.w	mds_update_seq_return

@cmd_other
	andi.w	#$1f,@cmd
	moveq	#0,@cmdlen
	move.b	@cmd_length_table(pc,@cmd),@cmdlen
	lsl.b	#2,@cmd
	pea		@next_command(pc)
	jmp		@cmd_table(pc,@cmd)

@cmd_drum_mode
	andi.w	#$00ff,@cmd
	clr.w	@tempreg
	move.b	t_stack_pos(twork),@tempreg			; push current pos
	move.w	@trackpos,t_stack(twork,@tempreg)
	addq.b	#2,@tempreg
	move.b	@tempreg,t_stack_pos(twork)
	lsl.w	#1,@cmd								; get subroutine pos
	move.w	0(tbase,@cmd),@trackpos
	moveq	#1,@cmdlen
	bra.w	@next_command

;----------------------------------------------------------------------
; command table
;----------------------------------------------------------------------
@cmd_length_table
	dc.b	1,2,2,2, 2,2,2,2, 2,2,2,2, 2,3,2,2		;e0
	dc.b	2,0,0,0, 0,0,3,0, 2,2,1,2, 2,3,1,0		;f0
@cmd_table
	bra.w	@cmd_slr						;e0 slur
	bra.w	@cmd_ins						;e1 instrument
	bra.w	@cmd_vol						;e2 volume
	bra.w	@cmd_volm						;e3 volume change
	bra.w	@cmd_trs						;e4 transpose
	bra.w	@cmd_trsm						;e5 transpose change
	bra.w	@cmd_dtn						;e6 detune
	bra.w	@cmd_pta						;e7 portamento
	bra.w	@cmd_peg						;e8 pitch envelope
	bra.w	@cmd_pan						;e9 panning
	bra.w	@cmd_lfo						;ea lfo sensitivity
	bra.w	@cmd_lfod						;eb lfo delay
	bra.w	@cmd_flg						;ec channel flags
	bra.w	@cmd_fmcreg						;ed fm channel reg write
	bra.w	@cmd_fmtl						;ee fm tl write
	bra.w	@cmd_fmtlm						;ef fm tl change
	bra.w	@cmd_pcm						;f0 pcm instrument
	bra.w	@cmd_finish						;f1
	bra.w	@cmd_finish						;f2
	bra.w	@cmd_finish						;f3
	bra.w	@cmd_finish						;f4
	bra.w	@cmd_finish						;f5
	bra.w	@cmd_fmreg						;f6 fm register write
	bra.w	@cmd_dmfinish					;f7 drum mode finish
	bra.w	@cmd_comm						;f8 communication byte
	bra.w	@cmd_tempo						;f9 tempo
	bra.w	@cmd_lp							;fa loop
	bra.w	@cmd_lpf						;fb loop finish
	bra.w	@cmd_lpb						;fc loop break
	bra.w	@cmd_lpbl						;fd loop break 2
	bra.w	@cmd_pat						;fe subroutine
@cmd_finish									;ff finish
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd
	bne		@cmd_return

	ori.l	#(nm_stop<<nf),flag
	move.b	#RCOUNT<<1,t_request_id(twork)	;track is free

	moveq	#-1,@tempreg					;clear channel mask
	bclr	tnum,@tempreg
	and.w	@tempreg,w_tmask(work,rnum)

	moveq	#-1,@tempreg					;deallocate channel
	move.b	t_channel_id(twork),@cmd
	bclr	@cmd,@tempreg
	and.w	@tempreg,w_chmask(work,rnum)

	addq.w	#4,sp							;break out of command
	move.w	@trackpos,t_position(twork)
	bra.w	mds_update_seq_return

@cmd_return
	subq.b	#2,@cmd
	move.b	@cmd,t_stack_pos(twork)
	move.w	t_stack(twork,@cmd),@trackpos	; pop return address
@cmd_dummy
	rts

;----------------------------------------------------------------------
; slr - set note slur
;----------------------------------------------------------------------
@cmd_slr
	bset	#nf+nf_slur,flag
	rts

;----------------------------------------------------------------------
; ins - set instrument (or PSG envelope)
;----------------------------------------------------------------------
; CAUTION: instrument data must fit within 32k of w_sdtop address
;  due to the "movea @cmd,tmpa0"
;----------------------------------------------------------------------
@cmd_ins
	move.b	0(tbase,@trackpos),@cmd
	move.b	@cmd,t_ins(twork)
	bset	#nf+nf_ins,flag
	bclr	#cf+cf_pcm_control,flag
	lsl.w	@cmd
	movea.w	0(tbase,@cmd),tmpa0				; instr address
	move.b	t_channel_id(twork),@cmd
	cmpi.b	#6,@cmd							; fm or psg
	bcc.s	@cmd_ins_psg
	adda.l	w_sdtop(work),tmpa0				; ptr. to ins data
	bset	#nf+nf_key_off,flag
	move.l	24(tmpa0),t_fm_tl(twork)
	move.b	28(tmpa0),t_fm_alg(twork)
	move.b	29(tmpa0),t_ins_trs(twork)
	st		t_last_pitch(twork)				; may reset pitch
	rts

@cmd_ins_psg
	move.w	tmpa0,t_psg_eg_addr(twork)
	cmpi.w	#$ff00,t_psg_eg_pos(twork)		; don't reset stopped env
	bcc.s	@cmd_dummy
	move.w	#$000f,t_psg_eg_pos(twork)
	rts

;----------------------------------------------------------------------
; vol - set volume
;----------------------------------------------------------------------
@cmd_vol
	move.b	0(tbase,@trackpos),t_vol(twork)
	bset	#nf+nf_vol,flag
	rts
@cmd_volm
	move.b	0(tbase,@trackpos),@cmd
	add.b	@cmd,t_vol(twork)
	bset	#nf+nf_vol,flag
	rts

;----------------------------------------------------------------------
; vol - set transpose
;----------------------------------------------------------------------
@cmd_trs
	move.b	0(tbase,@trackpos),t_trs(twork)
	rts
@cmd_trsm
	move.b	0(tbase,@trackpos),@cmd
	add.b	@cmd,t_trs(twork)
	rts

;----------------------------------------------------------------------
; dtn - set detune
;----------------------------------------------------------------------
@cmd_dtn
	move.b	0(tbase,@trackpos),t_dtn(twork)
	rts

;----------------------------------------------------------------------
; pta - set portamento
;----------------------------------------------------------------------
@cmd_pta
	move.b	0(tbase,@trackpos),t_pta(twork)
	rts

;----------------------------------------------------------------------
; peg - set pitch envelope
;----------------------------------------------------------------------
@cmd_peg
	clr.w	@cmd
	move.w	@cmd,t_peg_mod(twork)
	move.w	@cmd,t_peg_delay(twork)					;also clears pos
	move.b	0(tbase,@trackpos),@cmd
	beq.s	@cmd_peg_off
	lsl.w	#1,@cmd
	move.w	-2(tbase,@cmd),t_peg_addr(twork)
	rts
@cmd_peg_off
	move.w	@cmd,t_peg_addr(twork)
	rts

;----------------------------------------------------------------------
; pan - set panning
;----------------------------------------------------------------------
; bit 6 = enable left speaker
; bit 7 = enable right speaker
;----------------------------------------------------------------------
@cmd_pan
	move.b	t_channel_id(twork),@cmd
	cmpi.b	#6,@cmd							; fm only
	bcc.s	@cmd_pan_dummy

	move.b	t_fm_pan_lfo(twork),@cmd
	andi.b	#$3f,@cmd
	or.b	0(tbase,@trackpos),@cmd
	move.b	@cmd,t_fm_pan_lfo(twork)
	bset	#nf+nf_pan_lfo,flag
@cmd_pan_dummy
	rts

;----------------------------------------------------------------------
; lfo - set lfo depth (channel specific setting)
;----------------------------------------------------------------------
; bit 0-1 = FM sensitivity
; bit 3-5 = AM sensitivity
;----------------------------------------------------------------------
@cmd_lfo
	move.b	t_channel_id(twork),@cmd
	cmpi.b	#6,@cmd							; fm or psg
	bcc.s	@cmd_nmode

	move.b	t_fm_pan_lfo(twork),@cmd
	andi.b	#$c0,@cmd
	or.b	0(tbase,@trackpos),@cmd
	move.b	@cmd,t_fm_pan_lfo(twork)
	bset	#nf+nf_pan_lfo,flag
	rts

;----------------------------------------------------------------------
; nmode - set noise mode
;----------------------------------------------------------------------
; 00 - use key code
; e3 - periodic noise, psg3 control
; e7 - white noise, psg3 control
;----------------------------------------------------------------------
@cmd_nmode
	st		t_last_pitch(twork)
	move.b	0(tbase,@trackpos),@cmd
	move.b	@cmd,t_psg_nmode(twork)
	beq.s	@cmd_nmode_settrs
; TODO: currently force transpose for noise mode to match the ctrmml
; VGM converter
	moveq	#12,@cmd
@cmd_nmode_settrs
	move.b	@cmd,t_trs(twork)
	rts

;----------------------------------------------------------------------
; lfod - set lfo delay
;----------------------------------------------------------------------
@cmd_lfod
	move.b	0(tbase,@trackpos),t_lfo_delay(twork)
	rts

;----------------------------------------------------------------------
; flg - set channel flags
;----------------------------------------------------------------------
; 00-07 - clear flag
; 08-0F - set flag
; 80-8F - ch3 mask set
;----------------------------------------------------------------------
@cmd_flg
	st		t_last_pitch(twork)				; force pitch update
	clr.w	@cmd
	move.b	0(tbase,@trackpos),@cmd
	bmi.s	@cmd_flg_fm3					; bit7 set = fm3 flags
	bclr	#3,@cmd							; bit3 set = set a flag
	beq.s	@cmd_flg_off
	addi.b	#cf,@cmd
	bset	@cmd,flag
	rts
@cmd_flg_off
	addi.b	#cf,@cmd
	bclr	@cmd,flag
	rts
@cmd_flg_fm3
	beq.s	@cmd_flg_fm3_off
	bset	#cf+cf_fm3_enable,flag
	move.b	@cmd,t_fm3_mask(twork)	; we're reusing MSB of t_base_addr
	rts
@cmd_flg_fm3_off
	bclr	#cf+cf_fm3_enable,flag
	rts

;----------------------------------------------------------------------
; fmreg - write to FM chip
;----------------------------------------------------------------------
@cmd_fmreg
	mds_z80_wait_fm							; the panning
	move.b	0(tbase,@trackpos),$4000(zram)
	move.b	1(tbase,@trackpos),$4001(zram)
	mds_z80_start
	rts

@cmd_fmcreg
	rts

;----------------------------------------------------------------------
; fmtl - set FM TL
;----------------------------------------------------------------------
@cmd_fmtl
	rts
@cmd_fmtlm
	rts

;----------------------------------------------------------------------
; comm - communication byte
;----------------------------------------------------------------------
@cmd_comm
	rts

;----------------------------------------------------------------------
; tempo - set song tempo
;----------------------------------------------------------------------
@cmd_tempo
	clr.w	@cmd
	move.b	0(tbase,@trackpos),@cmd
	move.w	@cmd,w_tempo(work,rnum)
	rts

;----------------------------------------------------------------------
; lp - loop start
;----------------------------------------------------------------------
@cmd_lp
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd
	move.b	#$ff,t_stack(twork,@cmd)
	move.w	@trackpos,2+t_stack(twork,@cmd)
	addq.b	#4,@cmd
@cmd_lpf_exit
	move.b	@cmd,t_stack_pos(twork)
	rts

;----------------------------------------------------------------------
; lpf - loop finish
;----------------------------------------------------------------------
@cmd_lpf
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd
	subq.b	#4,@cmd
	move.b	t_stack(twork,@cmd),@tempreg
	cmpi.b	#$ff,@tempreg
	bne.s	@cmd_lpf_decrease
	move.b	0(tbase,@trackpos),@tempreg
@cmd_lpf_decrease
	subq.b	#1,@tempreg
	beq.s	@cmd_lpf_exit
	move.b	@tempreg,t_stack(twork,@cmd)
	move.w	2+t_stack(twork,@cmd),@trackpos
	moveq	#1,@cmdlen
	rts

;----------------------------------------------------------------------
; lpb - loop break
;----------------------------------------------------------------------
@cmd_lpb
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd
	subq.b	#4,@cmd  ;optimize with stack-4?
	cmpi.b	#1,t_stack(twork,@cmd)
	bne.s	@cmd_lpb_skip
	move.b	0(tbase,@trackpos),@cmdlen
	move.b	@cmd,t_stack_pos(twork)
@cmd_lpb_skip
	rts

;----------------------------------------------------------------------
; lpbl - loop break (long)
;----------------------------------------------------------------------
@cmd_lpbl
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd
	subq.b	#4,@cmd
	tst.b	t_stack(twork,@cmd)
	bne.s	@cmd_lpb_skip
	move.b	0(tbase,@trackpos),@tempreg ;no need to align 16-bit
	lsl.w	#8,@tempreg
	move.b	1(tbase,@trackpos),@tempreg
	move.w	@tempreg,@cmdlen
	move.b	@cmd,t_stack_pos(twork)
	rts

;----------------------------------------------------------------------
; pat - subroutine
;----------------------------------------------------------------------
@cmd_pat
	clr.w	@cmd
	move.b	t_stack_pos(twork),@cmd				; push current pos
	addq.w	#2,@trackpos
	move.w	@trackpos,t_stack(twork,@cmd)
	addq.b	#2,@cmd
	move.b	@cmd,t_stack_pos(twork)
	clr.w	@cmd
	move.b	-2(tbase,@trackpos),@cmd			; get subroutine pos
	lsl.w	#1,@cmd
	move.w	0(tbase,@cmd),@trackpos
	rts

;----------------------------------------------------------------------
; dmfinish - drum mode finish byte
;----------------------------------------------------------------------
@cmd_dmfinish
	clr.w	@cmd
	move.b	0(tbase,@trackpos),t_note(twork)
	move.b	t_stack_pos(twork),@cmd
	subq.b	#2,@cmd
	move.b	@cmd,t_stack_pos(twork)
	move.w	t_stack(twork,@cmd),@trackpos		; pop return address
	addq.w	#4,sp								;break out of command
	bra.w	@cmd_tie

;----------------------------------------------------------------------
; pcm - enable PCM
;----------------------------------------------------------------------
; CAUTION: this command must only be executed on FM6 or dummy channel
;----------------------------------------------------------------------
@cmd_pcm
	move.b	0(tbase,@trackpos),@cmd
	lsl.w	@cmd
	move.b	1(tbase,@cmd),t_ins(twork)			; instr address
	bset	#cf+cf_pcm_control,flag
	rts

;======================================================================
; check channel priority
;----------------------------------------------------------------------
; INPUT
;   d0 - channel id
; OUTPUT
;   d0 - request # with highest priority
; TRASHES
;	tmpa0, d1, d2
mds_check_priority
	move.w	d0,d2
	clr.w	d0
	lea		w_chmask(work),tmpa0
	rept	RCOUNT
		move.w	(tmpa0)+,d1
		btst	d2,d1
		bne		@end
		addq.b	#2,d0						;next request
	endr
@end
	rts

;======================================================================
; get FM pitch
;----------------------------------------------------------------------
; INPUT
;   d0 - key code + fraction
; OUTPUT
;   d0 - pitch value
; TRASHES
;   tmpa1, d1, d2
mds_get_fm_pitch
	lsl.l	#8,d0
	move.w	d0,d1							; upper bits of keycode
	swap	d0
	lea		mds_note_table(pc),tmpa1
	move.b	mds_octave_table-mds_note_table(tmpa1,d0),d2	; octave
	move.b	0(tmpa1,d0),d0
	add.b	t_ins_trs(twork),d0				;freq tab displacement
	lea		@fm_freq_tab(pc,d0),tmpa1
	move.w	2(tmpa1),d0
	sub.w	(tmpa1),d0
	mulu	d1,d0							;interpolate
	swap	d0
	add.w	(tmpa1),d0
	rol.w	#8,d0
	add.b	d2,d0							;octave added
	rts
@fm_freq_tab
	dc.w 161,171,181,191,203,215,228,241,255,271,287,304		; 0-11
	dc.w 322,341,361,383,406,430,455,482,511,541,574,608		; 12-23
	dc.w 644,682,723,766,811,859,910,965,1022,1083,1147,1215	; 24-35
	dc.w 1288,1364,1445,1531,1622,1719,1821,1929,2044			; 36-45

;======================================================================
; get PSG pitch
;----------------------------------------------------------------------
; INPUT
;   d0 - key code + fraction
; OUTPUT
;   d0 - pitch value
; TRASHES
;   tmpa0, d1, d2
mds_get_psg_pitch
	lsl.l	#8,d0
	move.w	d0,d1							; upper bits of keycode
	swap	d0
	lea		mds_note_table(pc),tmpa0
	move.b	mds_octave_table-mds_note_table(tmpa0,d0),d2	; octave
	move.b	0(tmpa0,d0),d0
	lea		@psg_freq_tab(pc,d0),tmpa0
	move.w	(tmpa0),d0
	sub.w	2(tmpa0),d0
	mulu	d1,d0							;interpolate
	swap	d0
	neg.w	d0
	add.w	(tmpa0),d0
	lsr.b	#3,d2
	lsr.w	d2,d0							;octave shifted
	rts
@psg_freq_tab
	dc.w	1710,1614,1524,1438,1357,1281,1209,1141,1077,1017,960,906
	dc.w	855

;======================================================================
; Note/octave division table
;----------------------------------------------------------------------
; note values are multiplied by 2 since they are likely going to be
; used as LUT indices
; Also the note table should come before the octave table, as the
; entries in the octave_table protect against odd memory address
; accesses.
mds_note_table
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;0
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;12
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;24
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;36
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;48
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;60
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;72
	dc.b	 0, 2, 4, 6, 8,10,12,14,16,18,20,22 ;84
mds_octave_table
	dcb.b   12,0  ;0  =0
	dcb.b	12,8  ;12 =1
	dcb.b	12,16 ;24 =2
	dcb.b	12,24 ;36 =3
	dcb.b	12,32 ;48 =4
	dcb.b	12,40 ;60 =5
	dcb.b	12,48 ;72 =6
	dcb.b	12,56 ;84 =7
; adding more octaves for PSG octave shift. It shouldn't matter that
; the note_table is overflowing into octave_table at these octaves.
	dcb.b	12,64 ;96 =8
	dcb.b	12,72 ;108=9
	dcb.b	12,80 ;120=10
; these bytes are just to pad the note_table, but it can be repurposed
; as long as the data just contains even 8-bit numbers
	dcb.b	256-(96+132),88

;======================================================================
; PSG envelope initialize
;----------------------------------------------------------------------
mds_psg_init
	dc.b	$10,$01,$1f,$00

;======================================================================
; dummy routine
;----------------------------------------------------------------------
mds_ch_dummy
	rts

;======================================================================
; Pitch update
;----------------------------------------------------------------------
; INPUT
;  work, twork
; OUTPUT
;  d0 - calculated pitch
; TRASHES
;  tmpa1,d1,d2,d3,d4
mds_pitch_update
	moveq	#0,d0
	move.b	t_note(twork),d1
	add.b	t_trs(twork),d1
	lsl.w	#8,d1
	move.b	t_dtn(twork),d0							; signed value
	ext.w	d0
	add.w	d1,d0									; d0=porta_target

;----------------------------------------------------------------------
; portamento update
;----------------------------------------------------------------------
	clr.w	d3
	move.b	t_pta(twork),d3
	beq.s	@porta_done

	move.w	d0,d2
	sub.w	t_pitch(twork),d2						;d2=porta_delta
	beq.s	@porta_done

	move.w	d2,d1									;get step
	asr.w	#8,d1
	bpl.s	@add_step
	subq.w	#2,d1
@add_step
	addq.w	#1,d1

	muls	d3,d1
	asr.w	#1,d1
	add.w	t_pitch(twork),d1						;d1=new_pitch

	move.w	d0,d3
	sub.w	d1,d3
	eor.w	d2,d3
	bmi.s	@porta_done
	move.w	d1,d0
@porta_done
	move.w	d0,t_pitch(twork)

;----------------------------------------------------------------------
; pitch envelope update
;----------------------------------------------------------------------
	move.w	t_peg_addr(twork),d1
	beq.s	@no_peg

	btst	#nf+nf_key_on,flag
	beq.s	@no_retrig
	clr.w	t_peg_delay(twork)
@no_retrig

	movea.w	d1,tmpa1
	adda.l	w_sdtop(work),tmpa1

	clr.w	d1
	move.b	t_peg_pos(twork),d1
	lsl.w	#2,d1
	move.w	t_peg_mod(twork),d2						;d2=modulator

	move.b	t_peg_delay(twork),d3
	bne.s	@no_reset
	move.w	0(tmpa1,d1),d2							;d2=init modulator

@no_reset
	move.b	2(tmpa1,d1),d4
	ext.w	d4
	add.w	d2,d4									;add delta
	move.w	d4,t_peg_mod(twork)
	add.w	d4,d0

	cmp.b	3(tmpa1,d1),d3							;delay done?
	bne.s	@delay_not_done

	move.w	4(tmpa1,d1),d2							;next command
	subi.w	#$7f00,d2
	blt.s	@no_jump

	move.w	d2,t_peg_delay(twork)					;jump to position
@no_peg
	rts

@no_jump
	lsr.w	#2,d1
	addq.b	#1,d1
	move.w	d1,t_peg_delay(twork)					;next position
	rts

@delay_not_done
	addq.b	#2,d3									;increase delay
	beq.s	@no_peg									; ff = endless
	subq.b	#1,d3
	move.b	d3,t_peg_delay(twork)
	rts

;======================================================================
; FM patch upload
;----------------------------------------------------------------------
; NOTE: does not wait for the FM busy flag to clear at the beginning
;       of this routine
; INPUT:
;  tmpa0 - FM write address
;  tmpa1 - instrument data address
;  chnid - FM logical channel #
; TRASHES
;  tmpa1,d0,d1,d2,d3
mds_fm_update_ins
	move.w	chnid,d1
	andi.b	#$03,d1
	swap	chnid

;----------------------------------------------------------------------
; channel mute
;----------------------------------------------------------------------
; optimization: rather than checking the busy flag we could just wait
; the mandated number of cycles between writes = 83 according to YM2608
; manual
	move.b	#$40,d0								;tl=max
	add.b	d1,d0
	moveq	#3,d2								;op count
	bra.s	@notbusy
@nextop											; burn 83-14 = 69
	bsr.s	@burn56								;     56 : 56
@notbusy:
	move.b	d0,0(zram,chnid)					;     14 : 70
	move.b	#$7f,1(zram,chnid)					;        : 18
	addq.b	#4,d0								;  10/14 : 18 (14)
	dbra	d2,@nextop							;  10/14 : 32

;----------------------------------------------------------------------
; load inst data
;----------------------------------------------------------------------
	lea		@op_reg_table(pc),tmpa0				;      8
	moveq	#5,d3								;      4

@nextreg
	move.b	(tmpa0)+,d0							;      8
	add.b	d1,d0								;      4
	moveq	#3,d2								;      4
@nextop2										; burn 83-14 = 69
	bsr.s	@burn56								;     56 : 56
	move.b	d0,0(zram,chnid)					;     14 : 70
	move.b	(tmpa1)+,1(zram,chnid)				;       : 18
	addq.b	#4,d0								;      4 :  4
	dbra	d2,@nextop2							;  10/14 : 18 (14)
	dbra	d3,@nextreg							;  10/14 : 32

;----------------------------------------------------------------------
; load feedback/algorithm
;----------------------------------------------------------------------
												; burn 83-32 = 51
	addq.w	#4,tmpa1							;      8 :  8
	move.b	#$b0,d0								;      8 : 16
	add.b	d1,d0								;      4 : 20
	cmp.b	sound_fm_a0,d0						;12+4=16 : 36
	move.b	d0,0(zram,chnid)					;     14 : 52
	move.b	(tmpa1)+,1(zram,chnid)				;        : 18
	swap	chnid
	rts

;----------------------------------------------------------------------
; operator register table
;----------------------------------------------------------------------
@op_reg_table
	dc.b	$30,$50,$60,$70,$80,$90

;----------------------------------------------------------------------
; burn cycles while FM chip is busy
;----------------------------------------------------------------------
@burn56											; bsr=18 : 18
	ror.w	#1,d0								;  6+4=8 : 26
	rol.w	#1,d0                               ;  6+4=8 : 34
	cmp.l	d0,d0								;      6 : 40
	rts											;     16 : 56

;======================================================================
; FM volume update
;----------------------------------------------------------------------
; NOTE: does not wait for the FM busy flag to clear at the beginning
;       of this routine
; INPUT:
;  tmpa0 - FM write address
;  chnid - FM logical channel #
; TRASHES
;  tmpa1,tmpa2,d0,d1,d2,d3,d4
mds_fm_update_vol
	lea		4+t_fm_tl(twork),tmpa1
	move.b	t_vol(twork),d3
	bpl.s	@no_vol_table
	cmpi.b	#$90,d3
	bcc.s	@no_vol_table
	andi.w	#$000f,d3
	move.b	mds_fm_vol_table(pc,d3),d3

@no_vol_table
	move.b	t_fm_alg(twork),d4
	andi.w	#$0007,d4

	moveq	#3,d2
	move.w	chnid,d1
	and.b	d2,d1
	add.b	#$4c,d1
	swap	chnid
@nextop											; burn 83-14 = 69
	move.b	-(tmpa1),d0							;      10 : 10
	cmp.b	mds_fm_op_table(pc,d4),d2			; 10+4=14 : 24
	bcs.s	@modulator							;    10/8 : 32
	add.b	d3,d0								;       4 : 36
	bpl.b	@no_overflow						;    10/8 : 44
	move.b	#$7f,d0								;       8 : 52
	bra.s	@write_vol							;      10 : 62
@modulator										;           34
	cmp.b	*(pc),d0							;  8+4=12 : 46
@no_overflow									;           46
	cmp.b	*(pc),d0							;  8+4=12 : 58
@write_vol										;           58 to 62
	move.b	d1,0(zram,chnid)					;      14 : 72-76
	nop											;       4 :  4
	move.b	d0,1(zram,chnid)					;      14 : 18
	subq.b	#4,d1								;       4 :  4
	dbra	d2,@nextop							;   10/14 : 18 (14)
	swap	chnid
	rts

;======================================================================
; FM volume calculation tables
;----------------------------------------------------------------------
mds_fm_op_table
	dc.b	3,3,3,3,2,1,1,0
mds_fm_vol_table
	dc.b	42,40,37,34,32,29,26,24,21,18,16,13,10,8,5,2

;======================================================================
; FM cutoff
;----------------------------------------------------------------------
; Assumes z80 is stopped and reg $28 is latched
mds_fm_cutoff
	move.b	chnid,$4001(zram)					;12 cycles + 2 nop

	move.w	chnid,d1
	andi.b	#$03,d1
	swap	chnid

	move.b	#$80,d0								;rr=max
	add.b	d1,d0
	moveq	#3,d2								;op count
	bra.s	@notbusy

@nextop											; burn 83-14 = 69
	ror.l	#8,d0								; 16+8=24 : 24
	rol.l	#6,d0                               ; 12+8=20 : 44
	rol.l	#2,d0								;  4+8=12 : 56

@notbusy:
	move.b	d0,0(zram,chnid)					;     14 : 70
	move.b	#$ff,1(zram,chnid)					;        : 18
	addq.b	#4,d0								;  10/14 : 18 (14)
	dbra	d2,@nextop							;  10/14 : 32
	swap	chnid

	mds_z80_start
	bra.w	mds_update_return

;======================================================================
; FM update
;----------------------------------------------------------------------
; INPUT:
;  work, twork, chnid
; TRASHES:
;  chnid
mds_fm6_update
	btst	#cf+cf_pcm_control,flag
	bne.w	mds_pcm1_update
	tst.b	w_pcm_enable(work)
	beq.s	mds_fm1_update

	clr.b	w_pcm_enable(work)			; send key off if we just
	mds_z80_cmd0	#zc_pcm1_stop		; switched from PCM to FM

mds_fm1_update
	bset	#17,chnid								; $4002
mds_fm0_update
@fm			equr	tmpa0
	bset	#30,chnid								; $4000
;----------------------------------------------------------------------
; key_off update
;----------------------------------------------------------------------
	bclr	#nf+nf_key_off,flag
	beq.s	@no_key_off

	mds_z80_wait_fm
	move.b	#$28,$4000(zram)

	tst.l	flag								; enable flag cleared?
	bpl.s	mds_fm_cutoff

	move.b	chnid,$4001(zram)						;12 cycles + 2 nop

@no_key_off
;----------------------------------------------------------------------
; instrument update
;----------------------------------------------------------------------
; this code should burn enough cycles (92) to make it necessary not to
; wait for busy flag in mds_fm_upload.
	bclr	#nf+nf_ins,flag
	beq.s	@no_ins

	move.b	t_ins(twork),d0
	lsl.w	d0
	move.l	t_base_addr(twork),tmpa1
	movea.w	0(tmpa1,d0),tmpa1
	adda.l	w_sdtop(work),tmpa1					; ptr. to ins data
	bsr.w	mds_fm_update_ins
	bclr	#nf+nf_vol,flag
	bra.s	@update_vol

@no_ins
;----------------------------------------------------------------------
; vol update
;----------------------------------------------------------------------
	bclr	#nf+nf_vol,flag
	beq.s	@no_vol
	mds_z80_wait_fm

@update_vol
	bsr.w	mds_fm_update_vol

@no_vol
;----------------------------------------------------------------------
; pitch update
;----------------------------------------------------------------------
	mds_z80_start
	bsr.w	mds_pitch_update
	cmp.w	t_last_pitch(twork),d0
	beq.s	@no_write_pitch

; this routine won't be truly useful until i have FM f-num
; calculation on Z80

;	move.w	d0,t_last_pitch(twork)
;	move.b	chnid,d1							; multiply by 3
;	add.b	d1,chnid
;	add.b	d1,chnid
;	mds_z80_cmd2	d1,d0						; pitch update

	move.w	d0,d3
	bsr.w	mds_get_fm_pitch
	move.b	chnid,d1
	swap	chnid
	andi.b	#3,d1
	addi.b	#$a4,d1
	mds_z80_wait_fm
	move.b	d1,0(zram,chnid)
	nop
	move.b	d0,1(zram,chnid)					; 4+14 : 18
	ror.w	#8,d0								; 22  :  22
	subq.b	#4,d1								;  4  :  28
	move.w	d3,t_last_pitch(twork)				; 12  :  40
	move.b	d1,0(zram,chnid)					; 14  :  54
	nop
	move.b	d0,1(zram,chnid)					; 4+14 : 18
	mds_z80_start
	swap	chnid

@no_write_pitch
;----------------------------------------------------------------------
; pan_lfo update
;----------------------------------------------------------------------
	bclr	#nf+nf_pan_lfo,flag
	beq.s	@no_lfo
	move.b	t_fm_pan_lfo(twork),d0
	move.b	chnid,d1
	swap	chnid
	andi.b	#3,d1
	addi.b	#$b4,d1
	mds_z80_wait_fm
	move.b	d1,0(zram,chnid)
	nop
	move.b	d0,1(zram,chnid)					; 4+14 : 18
	mds_z80_start
	swap	chnid

@no_lfo
;----------------------------------------------------------------------
; key_on update
;----------------------------------------------------------------------
	bclr	#nf+nf_key_on,flag
	beq.s	@no_key_on

	btst	#cf+cf_fm3_enable,flag
	beq.s	@no_fm3_key_on						; TODO: fm3_keyon

	bra.w	mds_update_return

@no_fm3_key_on
;	btst	#cf+cf_pcm_control,flag				; TODO: pcm_keyon

	bclr	#nf+nf_slur,flag
	bne.s	@no_key_on

	mds_z80_wait_fm
	move.b	#$28,$4000(zram)
	move.b	chnid,d0							; 4 :  4
	ori.b	#$f0,d0								; 8 : 12
	move.b	d0,$4001(zram)						;12 : 24
	mds_z80_start

@no_key_on
	bra.w	mds_update_return


;======================================================================
; PSG envelope update
;----------------------------------------------------------------------
; This code is unfortunately not as nice as it could have been...
; But to clear things up, this is the envelope state:
;  psg_eg_addr   - Always points to beginning of envelope
;  psg_eg_pos    - Envelope position. 0xff will stop the envelope.
;  psg_eg_delay  - Low 4 bits contain the current PSG volume level
;                  (where 0 is loudest and 15 is silent).
;                  If the high 4 bits are zero, wait for a key off
;                  event before reading the next byte. Otherwise, this
;                  is the amount of frames (minus 1) before the next
;                  byte is read from the envelope.
mds_psg_update_env
@pos	equr	d1
@cmd	equr	d0

	tst.l	flag								; enable flag cleared?
	bpl.w	@silence

;----------------------------------------------------------------------
; key on
;----------------------------------------------------------------------
	btst	#nf+nf_key_on,flag
	beq.s	@no_keyon
	bclr	#nf+nf_slur,flag
	bne.s	@no_keyon
	btst	#cf+cf_fm3_enable,flag
	bne.s	@no_keyon

	moveq	#$1f,@cmd								;silence
	move.w	@cmd,t_psg_eg_pos(twork)
	andi.l	#~((1<<(nf+nf_key_on))+(1<<(nf+nf_key_off))),flag
	bset	#nf+nf_sustain,flag

	st		t_last_pitch(twork)	; Always rewrite pitch after PSG keyon
	clr.w	t_peg_delay(twork)
	bra.s	@command

@no_keyon
;----------------------------------------------------------------------
; envelope disable
;----------------------------------------------------------------------
	btst	#nf+nf_key_off,flag					;skip delays for keyoff
	bne.s	@key_off
	btst	#nf+nf_sustain,flag
	beq.s	@command
	move.b	t_psg_eg_delay(twork),@cmd
	moveq	#$10,@pos
	sub.b	@pos,@cmd
	bcs.s	@hold

;----------------------------------------------------------------------
; envelope delay
;----------------------------------------------------------------------
	cmp.b	@pos,@cmd
	bcs.s	@command

	move.b	@cmd,t_psg_eg_delay(twork)
@hold
	bclr	#nf+nf_vol,flag
	bne.s	@write_vol
	rts

@key_off
;----------------------------------------------------------------------
; key_off
;----------------------------------------------------------------------
	andi.l	#~((1<<(nf+nf_sustain))+(1<<(nf+nf_key_off))),flag

@command
;----------------------------------------------------------------------
; read command
;----------------------------------------------------------------------
	clr.w	@pos
	move.b	t_psg_eg_pos(twork),@pos
	addq.b	#1,@pos
	beq.s	@silence							; position FF = silence
	move.w	t_psg_eg_addr(twork),tmpa1
	adda.l	w_sdtop(work),tmpa1
	move.b	-1(tmpa1,@pos),@cmd

;----------------------------------------------------------------------
; sustain command
;----------------------------------------------------------------------
	btst	#nf+nf_sustain,flag
	bne.s	@no_release
	cmpi.b	#$01,@cmd
	bne.s	@no_jump							; key off can't jump
	bset	#nf+nf_sustain,flag
	bra.s	@read_next

@no_release
	cmpi.b	#$01,@cmd
	bne.s	@no_sustain							; sustain hold
	clr.b	t_psg_eg_delay(twork)
	bra.s	@hold

@no_sustain
;----------------------------------------------------------------------
; jump command
;----------------------------------------------------------------------
	cmpi.b	#$02,@cmd
	bne.s	@no_jump
	move.b	0(tmpa1,@pos),@pos

@read_next
	move.b	0(tmpa1,@pos),@cmd
	addq.b	#1,@pos

@no_jump
;----------------------------------------------------------------------
; volume + length command
;----------------------------------------------------------------------
	cmpi.b	#$10,@cmd
	bcc.b	@no_silence
@silence
	move.w	#$ff0f,t_psg_eg_pos(twork)	; disable envelope
	bclr	#nf+nf_key_off,flag
	moveq	#$1f,@cmd
	or.b	chnid,@cmd
	move.b	@cmd,sound_psg
	rts

@no_silence
	lsl.w	#8,@pos
	move.b	@cmd,@pos
	move.w	@pos,t_psg_eg_pos(twork)	; write both at the same time
	bclr	#nf+nf_vol,flag

;----------------------------------------------------------------------
; It's very likely that this must be rewritten once global volume and
; fadeout is supported, and the FM->PSG volume conversion could be done
; at that point.
;----------------------------------------------------------------------
@tvol	equr	d1
@evol	equr	d0

@write_vol
	moveq	#15,d2
	move.b	t_vol(twork),@tvol
	not.b	@tvol
	and.b	d2,@tvol
	and.b	d2,@evol
	add.b	@evol,@tvol
	cmp.b	d2,@tvol
	bcs.s	@no_clamp
	move.b	d2,@tvol
@no_clamp
	ori.b	#$10,@tvol
	or.b	chnid,@tvol
	move.b	@tvol,sound_psg
	rts

;======================================================================
; PSG update
;----------------------------------------------------------------------
mds_psg_update
	bsr.w	mds_psg_update_env
	bsr.w	mds_pitch_update
	cmp.w	t_last_pitch(twork),d0
	beq.w	mds_update_return
	move.w	d0,t_last_pitch(twork)
mds_psg_set_pitch
	bsr.w	mds_get_psg_pitch
	move.b	d0,d1
	andi.b	#$0f,d1
	or.b	chnid,d1
	move.b	d1,sound_psg
	lsr.w	#4,d0
	andi.b	#$7f,d1
	move.b	d0,sound_psg
	bra.w	mds_update_return

;======================================================================
; PSG noise update
;----------------------------------------------------------------------
mds_psgn_update
	bsr.w	mds_psg_update_env
	bsr.w	mds_pitch_update
	cmp.w	t_last_pitch(twork),d0
	beq.w	mds_update_return
	move.w	d0,t_last_pitch(twork)
	move.b	t_psg_nmode(twork),d1
	beq.s	@no_psg3_control

	move.b	d1,sound_psg						; set noise mode
	move.b	#$c0,chnid							; set psg3 chnid
	bra.s	mds_psg_set_pitch

@no_psg3_control
	ror.w	#8,d0
	andi.b	#$07,d0
	ori.b	#$e0,d0
	move.b	d0,sound_psg
	bra.w	mds_update_return

;======================================================================
; Dummy channel update (only check for FM3)
;----------------------------------------------------------------------
mds_dummy_update
	btst	#cf+cf_pcm_control,flag
	bne.w	mds_pcm2_update
	tst.b	1+w_pcm_enable(work)
	beq.s	@no_pcm

	mds_z80_pcm_stop_i	1

@no_pcm
	bra.w	mds_update_return

;======================================================================
; PCM1 update
;----------------------------------------------------------------------
mds_pcm1_update
	moveq	#0,d0									;pcm1 offset
	tst.l	flag
	bmi.s	@no_disable
	clr.b	w_pcm_enable(work)
	bra.w	mds_pcm_key_off

@no_disable
	tst.b	w_pcm_enable(work)				; send key off if we just
	bne.s	@no_fm_key_off					; switched from FM to PCM

	st.b	w_pcm_enable(work)
	mds_z80_wait_fm
	move.b	#$28,$4002(zram)
	nop
	nop
	move.b	#$02,$4003(zram)						;12 cycles + 2 nop
	mds_z80_start

@no_fm_key_off
	bclr	#nf+nf_pan_lfo,flag
	beq.s	mds_pcm_update

	move.b	t_fm_pan_lfo(twork),d1			; FM6 can always control
	mds_z80_wait_fm							; the panning
	move.b	#$b6,$4002(zram)
	nop
	nop
	move.b	d1,$4003(zram)
	mds_z80_start
	bra.s	mds_pcm_update

;======================================================================
; PCM2 update
;----------------------------------------------------------------------
mds_pcm2_update
	moveq	#1,d0									;pcm2 offset
	st.b	1+w_pcm_enable(work)
	tst.l	flag
	bmi.s	mds_pcm_update
	clr.b	1+w_pcm_enable(work)
	bra.w	mds_pcm_key_off

;======================================================================
; PCM common update
;----------------------------------------------------------------------
mds_pcm_update
	bclr	#nf+nf_key_on,flag
	bne.s	mds_pcm_key_on
	bclr	#nf+nf_key_off,flag
	bne.w	mds_pcm_key_off
	bclr	#nf+nf_vol,flag
	beq.s	@no_set_vol

	move.b	#$9f,d1				; TODO: volume conversion for 00-7f
	sub.b	t_vol(twork),d1
	mds_z80_pcm_volume	d0,d1

@no_set_vol
	bra.w	mds_update_return

mds_pcm_key_on
	bclr	#nf+nf_vol,flag
	bclr	#nf+nf_key_off,flag
	move.b	#$9f,d1				; TODO: volume conversion for 00-7f
	sub.b	t_vol(twork),d1
	move.b	t_ins(twork),d2
	mds_z80_pcm_start_f	d0,d1,d2
	bra.w	mds_update_return

mds_pcm_key_off
	bclr	#nf+nf_vol,flag
	mds_z80_pcm_stop d0
	bra.w	mds_update_return

;======================================================================
; Z80 access functions
;======================================================================


;======================================================================
; Upload Z80 program. Assumes Z80 is already stopped
;----------------------------------------------------------------------
mds_z80_init
	move.l	a0,-(sp)					;backup work
	lea		@z80_driver(pc),a0			;upload Z80 driver
	lea		z80_ram,a1

	if PACK_Z80
		mds_z80_unpack
	else
		move.w	#@volume_table-@z80_driver-1,d0
@z80_loop
		move.b	(a0)+,(a1)+
		dbra	d0,@z80_loop
	endif

	lea		@volume_table(pc),a0
	lea		z80_ram+z_vtab,a1			; calculate volume tables
	moveq	#0,d0
@next_byte
	move.b	d0,d1
	eori.b	#$80,d1						;<-- for unsigned samples
	ext.w	d1
	muls	(a0),d1
	lsr.w	#8,d1
	move.b	d1,(a1)+
	addq.b	#1,d0
	bne.s	@next_byte
	cmpi.w	#8,(a0)+					; last entry?
	bne.s	@next_byte

	lea		z80_ram+z_stab,a1
	move.l	(sp)+,a0					; get work
	move.l	(a0),a0
	adda.l	-6(a0),a0					; get pcm table
	move.w	(a0)+,d0
	beq.s	@skip
	subq.w	#1,d0
@next_pcm
	move.l	(a0)+,d1					; get addr
	add.l	a2,d1
	lsl.l	#1,d1
	swap	d1
	move.b	#0,(a1)+					; bank
	move.b	d1,(a1)+
	swap	d1
	lsr.w	#1,d1
	ori.w	#$8000,d1
	move.b	d1,(a1)+					; address
	ror.w	#8,d1
	move.b	d1,(a1)+
	move.w	(a0)+,d1
	lsr.w	#5,d1						; div by 32
	addi.w	#$1ff,d1
	move.b	d1,(a1)+					; length
	ror.w	#8,d1
	move.b	d1,(a1)+
	dbra	d0,@next_pcm
@skip
	rts

@z80_driver

	if PACK_Z80
		incbin	"mdssub.slz"
	else
		incbin	"mdssub.bin"
	endif

	even
@volume_table
	dc.w	256,203,161,128,102,81,64,51,40,32,26,20,16,13,10,8

;----------------------------------------------------------------------
; vim: set ft=asm68k sw=4 ts=4 noet:
